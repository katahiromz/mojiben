; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3DF66435-0FE1-4E20-B7CE-72C48D809045}
AppName=Moji No Benkyou (ELG)
AppVerName={cm:AppName} 3.3
AppPublisher=katahiromz
VersionInfoProductName=Moji No Benkyou (ELG)
VersionInfoDescription=Moji No Benkyou (Elementary Low Graders)
VersionInfoCompany=katahiromz
VersionInfoVersion=3.3
VersionInfoCopyright=Copyright (C) 2009-2025 Katayama Hirofumi MZ. All rights reserved.
AppPublisherURL=https://katahiromz.fc2.page
AppSupportURL=https://katahiromz.fc2.page/moji-no-benkyou/
AppUpdatesURL=https://katahiromz.fc2.page/moji-no-benkyou/
DefaultDirName={pf}\Moji No Benkyou (ELG)
DefaultGroupName={cm:AppName}
DisableProgramGroupPage=yes
OutputDir=.
OutputBaseFilename=mojiben-ELG-3.3-setup
Compression=lzma
SolidCompression=yes
ShowLanguageDialog=auto

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"
Name: ja; MessagesFile: "compiler:Languages\Japanese.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "japanesefont"; Description: "{cm:InstallJapaneseFont}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "build\Release\mojiben1.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\mojiben2.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\mojiben3.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\mojiben4.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\mojiben5.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "build\Release\mojiben6.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "README.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "README-ENG.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "LICENSE.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "font\Piza-P-Gothic.ttf"; DestDir: "{fonts}"; FontInstall: "ピザPゴシック"; Flags: onlyifdoesntexist uninsneveruninstall; Tasks: japanesefont
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:ReadMeJPN}"; Filename: "{app}\README.txt"
Name: "{group}\{cm:ReadMeENG}"; Filename: "{app}\README-ENG.txt"
Name: "{group}\{cm:License}"; Filename: "{app}\LICENSE.txt"
Name: "{group}\(1) {cm:HiraganaAndKatakana}"; Filename: "{app}\mojiben1.exe"
Name: "{group}\(2) {cm:Alphabet}"; Filename: "{app}\mojiben2.exe"
Name: "{group}\(3) {cm:DigitsAndNumbers}"; Filename: "{app}\mojiben3.exe"
Name: "{group}\(4) {cm:Kanji1stGrader}"; Filename: "{app}\mojiben4.exe"
Name: "{group}\(5) {cm:Kanji2ndGrader}"; Filename: "{app}\mojiben5.exe"
Name: "{group}\(6) {cm:Kanji3rdGrader}"; Filename: "{app}\mojiben6.exe"
Name: "{group}\{cm:Uninstall}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{cm:MojiBen} (1) {cm:HiraganaAndKatakana}"; Filename: "{app}\mojiben1.exe"; Tasks: desktopicon
Name: "{commondesktop}\{cm:MojiBen} (2) {cm:Alphabet}"; Filename: "{app}\mojiben2.exe"; Tasks: desktopicon
Name: "{commondesktop}\{cm:MojiBen} (3) {cm:DigitsAndNumbers}"; Filename: "{app}\mojiben3.exe"; Tasks: desktopicon
Name: "{commondesktop}\{cm:MojiBen} (4) {cm:Kanji1stGrader}"; Filename: "{app}\mojiben4.exe"; Tasks: desktopicon
Name: "{commondesktop}\{cm:MojiBen} (5) {cm:Kanji2ndGrader}"; Filename: "{app}\mojiben5.exe"; Tasks: desktopicon
Name: "{commondesktop}\{cm:MojiBen} (6) {cm:Kanji3rdGrader}"; Filename: "{app}\mojiben6.exe"; Tasks: desktopicon

[CustomMessages]
ja.AppName=もじのべんきょう
en.AppName=Moji No Benkyou
ja.Authors=片山博文MZ
en.Authors=katahiromz
ja.ReadMeJPN=読んでね (日本語)
en.ReadMeJPN=ReadMe (Japanese)
ja.ReadMeENG=ReadMe (English)
en.ReadMeENG=ReadMe (English)
ja.License=ライセンス
en.License=License
ja.HiraganaAndKatakana=ひらがなとカタカナ
en.HiraganaAndKatakana=Hiragana and Katakana
ja.Alphabet=アルファベット
en.Alphabet=Alphabet
ja.DigitsAndNumbers=すうじとかず
en.DigitsAndNumbers=Digits and numbers
ja.Kanji1stGrader=小学校一年生でならうかん字
en.Kanji1stGrader=1st-Grader's Kanji
ja.Kanji2ndGrader=小学校二年生でならうかん字
en.Kanji2ndGrader=2nd-Grader's Kanji
ja.Kanji3rdGrader=小学校三年生でならうかん字
en.Kanji3rdGrader=3rd-Grader's Kanji
ja.Uninstall=アンインストール
en.Uninstall=Uninstall
ja.MojiBen=もじべん
en.MojiBen=MojiBen
ja.URL=https://katahiromz.fc2.page/mojiben/
en.URL=https://katahiromz.fc2.page/moji-no-benkyou/
ja.InstallJapaneseFont=日本語フォント (ピザPゴシック) をインストールする
en.InstallJapaneseFont=Install Japanese font (Piza P Gothic)
